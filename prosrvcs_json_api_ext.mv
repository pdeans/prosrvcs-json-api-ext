<MvFUNCTION NAME = "Module_Description" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.module:code"        VALUE = "prosrvcs_json_api_ext">
	<MvASSIGN NAME = "l.module:name"        VALUE = "Miva Professional Services JSON API Extensions">
	<MvASSIGN NAME = "l.module:provider"    VALUE = "Miva Professional Services">
	<MvASSIGN NAME = "l.module:version"     VALUE = "1.0000">
	<MvASSIGN NAME = "l.module:api_ver"     VALUE = "9.12">
	<MvASSIGN NAME = "l.module:description" VALUE = "Use this module for extending the list of available functions for the Miva JSON API. See the list of available extension functions at https://github.com/pdeans/prosrvcs-json-api-ext.">
	<MvASSIGN NAME = "l.module:features"    VALUE = "json_api">
</MvFUNCTION>

<MvFUNCTION NAME = "Module_JSON_API" PARAMETERS = "module var, function" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME="l.ok" VALUE="{ miva_output_header( 'Content-Type', 'application/json' ) }" >

	<MvIF EXPR = "{ l.function EQ 'Category_Load_Canonical' }">
		<MvFUNCTIONRETURN VALUE = "{ JSON_Category_Load_Canonical( l.module ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( 'MOD-PSJ-0001', 'Invalid function' ) }">
</MvFUNCTION>

<MvFUNCTION NAME="JSON_Category_Load_Canonical" PARAMETERS="module var" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvIF EXPR = "{ g.Session_Type NE 'api' }">								<MvFUNCTIONRETURN>																	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Store_Open() }">				<MvFUNCTIONRETURN>																	</MvIF>
	<MvIF EXPR = "{ NOT [ g.Module_Admin ].CanI( 'CTGY', 1, 0, 0, 0 ) }">	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Access_Denied() }">	</MvIF>

	<MvIF EXPR="{ NOT [ g.Module_JSON ].JSON_Category_Load( l.category ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvIF EXPR="{ NOT [ g.Module_Feature_URI_DB ].URI_Load_Category_Canonical( l.category:id, l.canonical_uri ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_Error( g.Error_Code, g.Error_Message ) }">
	</MvIF>

	<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Response_Start() }">
	{
		"category_id": <MvEVAL EXPR = "{ int( l.category:id ) }">,
		"canonical_uri": "<MvEVAL EXPR = "{ [ g.Module_JSON ].JSON_Encode( l.canonical_uri:uri ) }">"
	}
	<MvFUNCTIONRETURN VALUE = "{ [ g.Module_JSON ].JSON_Response_End() }">
</MvFUNCTION>